plugins {
    id 'java'
    id 'idea'
    id 'findbugs'
    id 'pmd'
    id 'checkstyle'
    id 'jacoco'
    id 'maven'
    id 'com.ofg.uptodate' version '1.6.2'
}

ext {
    excludeFromCoverage = ['**/Server*', '**/Operation*']
}

apply from: 'gradle/validation.gradle'
apply from: 'gradle/coverage.gradle'

group 'com.github.slamdev'
version '1.0.0-SNAPSHOT'

sourceCompatibility = 1.8

idea {
    project {
        ipr {
            withXml { provider ->
                def projectXml = provider.asNode()
                def compilerConfiguration = projectXml.component.find { component ->
                    component.'@name' == 'CompilerConfiguration'
                }
                def currentAnnotationProcessing = compilerConfiguration.annotationProcessing
                currentAnnotationProcessing.replaceNode {
                    annotationProcessing {
                        profile(name: 'Default', default: true, enabled: true) {
                            processorPath(useClasspath: true)
                        }
                    }
                }
            }
        }
    }
}

repositories {
    mavenCentral()
    jcenter()
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14.1'
}

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.21'
    compile 'ch.qos.logback:logback-classic:1.1.7'
    compile 'org.projectlombok:lombok:1.16.10'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
}
